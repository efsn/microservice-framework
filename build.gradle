/**
 buildscript的类型为script block（最上层），用于配置gradle的project实例
 API文档为https://docs.gradle.org/current/dsl/org.gradle.api.Project.html#org.gradle.api.Project:buildscript(groovy.lang.Closure)
 其余的根script block有"allprojects", "dependencies", "configurations"等，更多的可见https://docs.gradle.org/current/dsl/的“Build script structure”一节。
 Script Block是一种method的调用，传入的参数为configuration closure。执行后会对Project的属性进行配置
 此处的"buildscript"用于配置Project的build script的classpath
 */

buildscript {
    ext {
        springBootVersion = "2.0.2.RELEASE"
    }

    repositories {
        mavenLocal()
        mavenCentral()
//        maven{
//            url("http://maven.aliyun.com/nexus/content/repositories/central")
//        }
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.5"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "com.google.gradle:osdetector-gradle-plugin:1.4.0"
    }
}

apply from: "build-dependencies.gradle"

subprojects {

    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "idea"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.google.protobuf"

    group = "cn.elmi.microservice"
    version = "1.0-SNAPSHOT"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, compileTestJava, javadoc]*.options*.encoding = "UTF-8"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations {
        all*.exclude module: "commons-logging"
    }

    dependencyManagement {
        imports {
            mavenBom lib.spring.boot.dependencies
        }
    }

    jar {
        enabled = true
    }

    // springboot2.0 new feature
    bootJar {
        manifest {
            attributes "Start-Class": "cn.elmi.Application"
        }
    }

    dependencies {
        compile lib.spring.boot.starter.core,
                lib.lombok,
                lib.slf4j,
                lib.logback.classic,
                lib.logback.core

        testCompile lib.testng
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:$protobufVersion"
        }

        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
            }
        }

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    java {}
                }

                task.plugins {
                    grpc {

                    }
                }
            }
        }

        generatedFilesBaseDir = "$projectDir/src-gen"
    }

    clean {
        delete protobuf.generatedFilesBaseDir
    }

    task protoJar(type: Jar, dependsOn: ["build"]) {
        archiveName = "example-proto-${version}.jar"
        from("build/classes/java/main/")
        destinationDir = file("build/libs")
        include("cn/elmi/grpc/**/*.class")
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "http://10.72.46.201:8080/content/repositories/snapshots/")
                repository(url: "http://10.72.46.201:8080/content/repositories/releases/")
            }
        }
    }

}
